MYSQL_STORAGE_ENGINE(innobase,  innodb, [InnoDB Storage Engine],
        [Transactional Tables using InnoDB], [max,max-no-ndb])
MYSQL_PLUGIN_DIRECTORY(innobase, [storage/innobase])
MYSQL_PLUGIN_STATIC(innobase,   [libinnobase.a])
MYSQL_PLUGIN_DYNAMIC(innobase,  [ha_innodb.la])
MYSQL_PLUGIN_ACTIONS(innobase,  [
  AC_CHECK_HEADERS(sched.h)
  AC_CHECK_SIZEOF(int, 4)
  AC_CHECK_SIZEOF(long, 4)
  AC_CHECK_SIZEOF(void*, 4)
  AC_CHECK_FUNCS(sched_yield fdatasync localtime_r)
  AC_C_BIGENDIAN
  case "$target_os" in
         lin*)
           INNODB_CFLAGS="-DUNIV_LINUX";;
         hpux10*)
           INNODB_CFLAGS="-DUNIV_MUST_NOT_INLINE -DUNIV_HPUX -DUNIV_HPUX10";;
         hp*)
           INNODB_CFLAGS="-DUNIV_MUST_NOT_INLINE -DUNIV_HPUX";;
         aix*)
           INNODB_CFLAGS="-DUNIV_AIX";;
         irix*|osf*|sysv5uw7*|openbsd*)
           INNODB_CFLAGS="-DUNIV_MUST_NOT_INLINE";;
         *solaris*|*SunOS*)
           INNODB_CFLAGS="-DUNIV_SOLARIS";;
  esac
  INNODB_DYNAMIC_CFLAGS="-DMYSQL_DYNAMIC_PLUGIN"
  case "$target_cpu" in
	x86_64)
		# The AMD64 ABI forbids absolute addresses in shared libraries
		;;
	*86)
		# Use absolute addresses on IA-32
		INNODB_DYNAMIC_CFLAGS="$INNODB_DYNAMIC_CFLAGS -prefer-non-pic"
		;;
  esac
  AC_SUBST(INNODB_CFLAGS)
  AC_SUBST(INNODB_DYNAMIC_CFLAGS)
  ])

