# * Wait_for_status_variable
# * Assert_status_variable
# * Assert_received_notices
# * Assert_set_like_status_variable
# * Assert_client_message_count

-->macro Wait_for_status_variable  %VARIABLE_NAME%  %EXPECTED_VALUE%
quiet;
-->echo Verify [Status variable "%VARIABLE_NAME%" needs to have a value of %EXPECTED_VALUE%]
wait_for %EXPECTED_VALUE%
	SELECT VARIABLE_VALUE FROM performance_schema.session_status WHERE VARIABLE_NAME LIKE '%VARIABLE_NAME%';
noquiet;
-->endmacro

-->macro Wait_for_global_status_variable  %VARIABLE_NAME%  %EXPECTED_VALUE%
quiet;
-->echo Verify [Global status variable "%VARIABLE_NAME%" needs to have a value of %EXPECTED_VALUE%]
wait_for %EXPECTED_VALUE%
	SELECT VARIABLE_VALUE FROM performance_schema.global_status WHERE VARIABLE_NAME LIKE '%VARIABLE_NAME%';
noquiet;
-->endmacro


-->macro Assert_global_status_variable  %VARIABLE_NAME%  %EXPECTED_VALUE%
-->stmtsql SHOW GLOBAL STATUS LIKE '%VARIABLE_NAME%';
-->recvtovar %VARIABLE_VALUE% Value
-->echo Verify [Status variable "%VARIABLE_NAME%" needs to have a value of %EXPECTED_VALUE%]
-->assert_eq %EXPECTED_VALUE%	%VARIABLE_VALUE%
-->endmacro


-->macro Assert_status_variable  %VARIABLE_NAME%  %EXPECTED_VALUE%
-->stmtsql SHOW STATUS LIKE '%VARIABLE_NAME%';
-->recvtovar %VARIABLE_VALUE% Value
-->echo Verify [Status variable "%VARIABLE_NAME%" needs to have a value of %EXPECTED_VALUE%]
-->assert_eq %EXPECTED_VALUE%	%VARIABLE_VALUE%
-->endmacro

-->macro Assert_set_like_status_variable  %VARIABLE_NAME%  %DELIMITER%	%EXPECTED_VALUE%
quiet;
-->stmtsql  select  FIND_IN_SET("%EXPECTED_VALUE%", REPLACE(VARIABLE_VALUE,"%DELIMITER%",","))>0 as Value from performance_schema.session_status where VARIABLE_NAME like '%VARIABLE_NAME%';
-->recvtovar %RESULT% Value
-->echo Verify [Status variable "%VARIABLE_NAME%" needs to have a value of %EXPECTED_VALUE%]
-->assert_eq 1	%RESULT%
noquiet;
-->endmacro

-->macro Assert_session_variable  %VARIABLE_NAME%  %EXPECTED_VALUE%
-->stmtsql SHOW SESSION VARIABLES LIKE '%VARIABLE_NAME%';
-->recvtovar %VARIABLE_VALUE% Value
-->echo Verify [Session variable "%VARIABLE_NAME%" needs to have a value of %EXPECTED_VALUE%]
-->assert_eq %EXPECTED_VALUE%	%VARIABLE_VALUE%
-->endmacro


-->macro Assert_received_notices
-->received Mysqlx.Notice.Warning	%MYSQLXTEST_NO_OF_WARNINGS%
-->received Mysqlx.Notice.GroupReplicationStateChanged	%MYSQLXTEST_NO_OF_GLOBAL%
-->received Mysqlx.Notice.ServerHello	%MYSQLXTEST_NO_OF_SERVER_HELLOS%
-->varlet %ALL_GLOBALS% %MYSQLXTEST_NO_OF_SERVER_HELLOS%
-->varinc %ALL_GLOBALS% %MYSQLXTEST_NO_OF_GLOBAL%
-->callmacro Assert_status_variable	Mysqlx_notice_global_sent	%ALL_GLOBALS%
-->callmacro Assert_status_variable	Mysqlx_notice_warning_sent	%MYSQLXTEST_NO_OF_WARNINGS%

-->received Mysqlx.Notice.Frame	%MYSQLXTEST_NO_OF_OTHER%
-->varinc %MYSQLXTEST_NO_OF_OTHER% -%MYSQLXTEST_NO_OF_WARNINGS%
-->varinc %MYSQLXTEST_NO_OF_OTHER% -%ALL_GLOBALS%
-->callmacro Assert_status_variable	Mysqlx_notice_other_sent	%MYSQLXTEST_NO_OF_OTHER%
-->endmacro


-->macro Assert_client_message_count	%NAME%	%OP%	%COUNT%
noquery_result;
echo Verify [Client handled %NAME% message count is "%OP%" %COUNT%];
-->received %NAME%	%MYSQLXTEST_NO_OF_MESSAGES%
-->assert %MYSQLXTEST_NO_OF_MESSAGES%	%OP%	%COUNT%
query_result;
-->endmacro